let
    // configurations start
    Today = Date.From(DateTime.LocalNow()), // today's date
    FromYear = 2020, // set the start year of the date dimension; dates start from 1st of January of this year
    ToYear = 2030, // set the end year of the date dimension; dates end at 31st of December of this year
    StartOfFiscalYear = 7, // set the month number that is start of the financial year, for example; if fiscal year start is July, value is 7
    firstDayOfWeek = Day.Monday, // set the week's start day, values: Day.Monday, Day.Sunday...
    // configuration end

    FromDate = #date(FromYear, 1, 1),
    ToDate = #date(ToYear, 12, 31),
    Source = List.Dates(
        FromDate,
        Duration.Days(ToDate - FromDate) + 1,
        #duration(1, 0, 0, 0)
    ),
    #"Converted to Table" = Table.FromList(Source, Splitter.SplitByNothing(), null, null, ExtraValues.Error),
    #"Renamed Columns" = Table.RenameColumns(#"Converted to Table", {{"Column1", "Date"}}),
    #"Changed Type" = Table.TransformColumnTypes(#"Renamed Columns", {{"Date", type date}}),
    #"Inserted Year" = Table.AddColumn(#"Changed Type", "Year", each Date.Year([Date]), Int64.Type),
    #"Inserted Month" = Table.AddColumn(#"Inserted Year", "Month Number", each Text.PadStart(Text.From(Date.Month([Date])), 2, "0"), type text),
    #"Inserted YearMonth Number" = Table.AddColumn(#"Inserted Month", "YearMonth Number", each Text.From(Date.Year([Date])) & Text.PadStart(Text.From(Date.Month([Date])), 2, "0"), type text),
    #"Inserted Month Name Short" = Table.AddColumn(#"Inserted YearMonth Number", "Month Name Short", each Date.ToText([Date], "MMM"), type text),
    #"Inserted Month Name Long" = Table.AddColumn(#"Inserted Month Name Short", "Month Name Long", each Date.ToText([Date], "MMMM"), type text)
in
    #"Inserted Month Name Long"
